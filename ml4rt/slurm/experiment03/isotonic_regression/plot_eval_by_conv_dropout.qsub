#!/bin/tcsh

#SBATCH --job-name="plot_eval_by_non_conv_dropout"
#SBATCH --partition="fge"
#SBATCH --account="rda-ghpcs"
#SBATCH --qos="batch"
#SBATCH --ntasks=1
#SBATCH --nodes=1
#SBATCH --cpus-per-task=1
#SBATCH --mem=8G
#SBATCH --time=02:00:00
#SBATCH --array=1-36
##SBATCH --exclude=h31n03,h28n12
##SBATCH --exclude=h29n01,h26n05,h28n06,h29n07,h30n03,h30n15
#SBATCH --mail-type=BEGIN,END,FAIL
#SBATCH --mail-user=ryan.lagerquist@noaa.gov
#SBATCH --output=plot_eval_by_non_conv_dropout_%A_%a.out

module load cuda/10.1
source /scratch2/BMC/gsd-hpcs/Jebb.Q.Stewart/conda3.7/etc/profile.d/conda.csh
conda activate base

set CODE_DIR_NAME="/scratch1/RDARCH/rda-ghpcs/Ryan.Lagerquist/ml4rt_standalone_sr/ml4rt"
set TOP_MODEL_DIR_NAME="/scratch1/RDARCH/rda-ghpcs/Ryan.Lagerquist/ml4rt_models/experiment03"

set UPCONV_LAYER_DROPOUT_RATES=(-1.00 0.05 0.10 0.15 0.20 0.25 -1.00 0.05 0.10 0.15 0.20 0.25 -1.00 0.05 0.10 0.15 0.20 0.25 -1.00 0.05 0.10 0.15 0.20 0.25 -1.00 0.05 0.10 0.15 0.20 0.25 -1.00 0.05 0.10 0.15 0.20 0.25)
set SKIP_LAYER_DROPOUT_RATES=(-1.00 -1.00 -1.00 -1.00 -1.00 -1.00 0.05 0.05 0.05 0.05 0.05 0.05 0.10 0.10 0.10 0.10 0.10 0.10 0.15 0.15 0.15 0.15 0.15 0.15 0.20 0.20 0.20 0.20 0.20 0.20 0.25 0.25 0.25 0.25 0.25 0.25)

set upconv_layer_dropout_rate=${UPCONV_LAYER_DROPOUT_RATES[$SLURM_ARRAY_TASK_ID]}
set skip_layer_dropout_rate=${SKIP_LAYER_DROPOUT_RATES[$SLURM_ARRAY_TASK_ID]}

set upconv_layer_dropout_string=`printf "%.2f" $upconv_layer_dropout_rate`
set skip_layer_dropout_string=`printf "%.2f" $skip_layer_dropout_rate`

set conv_layer_dropout_strings=("-1.00" "0.05" "0.10" "0.15" "0.20" "0.25")
set line_styles_string="solid dashed solid dashed solid dashed"
set line_colours_string="27_158_119 27_158_119 217_95_2 217_95_2 117_112_179 117_112_179"
set line_legends_string="Conv_dropout_=_0.00 Conv_dropout_=_0.05 Conv_dropout_=_0.10 Conv_dropout_=_0.15 Conv_dropout_=_0.20 Conv_dropout_=_0.25"

set evaluation_file_names_string=""
set i=1

while ($i <= ${#conv_layer_dropout_strings})
    set this_directory_name="${TOP_MODEL_DIR_NAME}/conv-dropout=${conv_layer_dropout_strings[$i]}_upconv-dropout=${upconv_layer_dropout_string}_skip-dropout=${skip_layer_dropout_string}"
    set these_model_file_names={${this_directory_name}/model*.h5}
    set this_model_file_name=${these_model_file_names[$#these_model_file_names]}
    set this_model_dir_name=`echo $this_model_file_name | rev | cut -c 4- | rev`
    set evaluation_file_names_string="${evaluation_file_names_string} ${this_model_dir_name}/isotonic_regression/validation/evaluation.nc"
    
    @ i = $i + 1
end

set evaluation_dir_name="${TOP_MODEL_DIR_NAME}/isotonic_regression/upconv-dropout=${upconv_layer_dropout_string}_skip-dropout=${skip_layer_dropout_string}"

python3 -u "${CODE_DIR_NAME}/plot_evaluation.py" \
--input_eval_file_names ${evaluation_file_names_string} \
--line_styles ${line_styles_string} \
--line_colours ${line_colours_string} \
--line_legend_strings ${line_legends_string} \
--use_log_scale=1 \
--plot_by_height=1 \
--output_dir_name="${evaluation_dir_name}"
